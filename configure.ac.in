dnl
dnl Copyright (c) 2003 - 2022 The Xfce development team
dnl
dnl 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl 2010 Florian Rivoal <frivoal@xfce.org>
dnl 2012, 2014, 2016 Landry Breuil <landry@xfce.org>
dnl

dnl Version information
m4_define([systemload_version_major], [1])
m4_define([systemload_version_minor], [3])
m4_define([systemload_version_micro], [1])
m4_define([systemload_version_build], [@REVISION@])
m4_define([systemload_version_tag], [git])
m4_define([systemload_version], [systemload_version_major().systemload_version_minor().systemload_version_micro()ifelse(systemload_version_tag(), [git], [systemload_version_tag()-systemload_version_build()], [systemload_version_tag()])])

m4_define([systemload_debug_default], [ifelse(systemload_version_tag(), [git], [yes], [minimum])])

dnl Initialize autoconf
AC_COPYRIGHT([Copyright (c) 2006-2022 The Xfce development team. All rights reserved.])
AC_INIT([xfce4-systemload-plugin], [systemload_version], [https://gitlab.xfce.org/panel-plugins/xfce4-systemload-plugin/], [xfce4-systemload-plugin])
AC_PREREQ([2.69])
AC_CONFIG_MACRO_DIRS([m4])

dnl Initialize automake
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar no-dist-gzip foreign])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
AM_SILENT_RULES([yes])

dnl Check for basic programs
AC_PROG_CC()
AC_PROG_CXX()
LT_PATH_LD([])
AC_PROG_INSTALL
AX_CXX_COMPILE_STDCXX_11()
IT_PROG_INTLTOOL([0.35.0])

dnl Disable static libs
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl Check for kvm, needed for BSD
AC_CHECK_LIB([kvm], [kvm_open])

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for required packages
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.16.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.16.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.17.2])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.14.0])
XDT_CHECK_OPTIONAL_PACKAGE([LIBGTOP], [libgtop-2.0], [2.32.0], [libgtop],
                           [libgtop for network utilization monitoring])
XDT_CHECK_OPTIONAL_PACKAGE([UPOWER_GLIB], [upower-glib], [0.9.0], [upower],
                           [upower for adapting update interval to power state])

dnl Check for debugging support
XDT_FEATURE_DEBUG([systemload_debug_default])

AC_CONFIG_FILES([
Makefile
icons/Makefile
icons/16x16/Makefile
icons/32x32/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
panel-plugin/Makefile
po/Makefile.in
xfce4++/Makefile
xfce4++/util/Makefile
])
AC_OUTPUT
