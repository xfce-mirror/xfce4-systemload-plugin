dnl xfce4-systemload-plugin - Systemload monitor plugin for xfce4-panel
dnl
dnl 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl 2010 Florian Rivoal <frivoal@xfce.org>
dnl 2012, 2014, 2016 Landry Breuil <landry@xfce.org>

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([systemload_version_major], [1])
m4_define([systemload_version_minor], [1])
m4_define([systemload_version_micro], [2])
m4_define([systemload_version_build], [r@REVISION@])
m4_define([systemload_version_tag], [])
m4_define([systemload_version], [systemload_version_major().systemload_version_minor().systemload_version_micro()ifelse(systemload_version_tag(), [git], [systemload_version_tag()-systemload_version_build()], [systemload_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2007, 2014, 2016
        The Xfce development team. All rights reserved.])
AC_INIT([xfce4-systemload-plugin], [systemload_version], [http://bugzilla.xfce.org/], [xfce4-systemload-plugin])
AC_PREREQ([2.50])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar no-dist-gzip])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl ***************************
dnl *** Disable static libs ***
dnl ***************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl *************************************
dnl *** Check for kvm, needed for BSD ***
dnl *************************************
AC_CHECK_LIB([kvm], [kvm_open])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_OPTIONAL_PACKAGE([UPOWER_GLIB], [upower-glib], [0.9.0], [upower],
                           [upower for adapting update interval to power state])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
panel-plugin/Makefile
po/Makefile.in
])

